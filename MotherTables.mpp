/*NOTE(DurationOfLife, EN)
	This table contains statistics related to the duration of life.
*/

table Mother DurationOfLife //EN Duration of Life
{
	{
		value_in(alive),				//EN Population size
		min_value_out(duration()),		//EN Minimum duration of life decimals=4
		max_value_out(duration()),		//EN Maximum duration of life decimals=4
		duration() / value_in(alive)	//EN Life expectancy decimals=4

	}	//EN Demographic characteristics
};

table Mother Incidence //EN Incidence amongst mothers
{
	integer_age * //EN Age
	{
		undergone_transition( infected, FALSE, TRUE), //EN Number of new infections
		duration(), //EN Mother years in this age decimals=1
		(undergone_transition( infected, FALSE, TRUE) / unit) //EN Incidence decimals=4
	} //EN Infection Statistics
};

table Mother Mortality
{
	integer_age+ *
	{
		duration(), //EN Total Population
		duration(mdeath_sched_status, mdsHEALTHY), //EN Healthy Population
		undergone_transition(mdeath_sched_status, mdsHEALTHY, mdsDEAD), //EN Death from Healthy State
		duration(mdeath_sched_status, mdsINFECTED), //EN Infected Population
		undergone_transition(mdeath_sched_status, mdsINFECTED, mdsDEAD), //EN Death from Infected States
		duration(mdeath_sched_status, mdsDEAD) //EN Dead Population
	}
};

table Mother ModelVersion //EN Model Version
{
	{
		min_value_out(model_version)
	}
};