/* ################################################################################
The child distribution tables
################################################################################ */

table Child ChildMotherAgeAtBirthDistribution //EN The age of the mothers at birth of children
	[mother_age_at_birth > 0]
{
	split(mother_age_at_birth, TEN_YEAR_BINS)+ //EN Mother Age
		* 
	{
		unit //EN Number of Mothers
	}
};

table Child ChildGenderDistribution //EN The gender ratio of the children
{
	gender
		*
	{
		unit //EN Number of Children
	}
};

table Child ChildSESDistribution //EN The SES ratio of the children
	[alive == FALSE]
{
	ses
		*
	{
		unit //EN Number of Children
	}
};

table Child ChildSchoolStartAgeDistribution //EN The distribution of the school starting ages of the children
{
	integer_age //EN School Start Age
		*
	{
		undergone_transition(school_status, NOT_IN_SCHOOL, IN_SCHOOL) //EN Number of Children
	}
};

table Child ChildSchoolStartMDDistribution //EN The distribution of the childrens mother's disease statuses at school start 
{
	mdetailed_status //EN Mother Status
		*
	{
		undergone_transition(school_status, NOT_IN_SCHOOL, IN_SCHOOL) //EN Number of Children
	}
};

table_group Child_Attribute_Distribution
{
	ChildMotherAgeAtBirthDistribution,
	ChildGenderDistribution,
	ChildSESDistribution,
	ChildSchoolStartAgeDistribution,
	ChildSchoolStartMDDistribution
};

/* ################################################################################
Child General tables
################################################################################ */

table Child ChildSurvival //EN Number of child years accrued in each age
[alive == TRUE]
{
	gender
		*
	integer_age
		*
	{
		duration() //EN Child Years
	}
};

table Child ChildMotherStateAtDropout //EN State of mother when Child Drops Out
{
	child_sched_status + //EN Child Status
		*
	{
		undergone_transition(school_status, IN_SCHOOL, DROPOUT) //EN Number of Dropouts
	}
};

table Child ChildFinalSchoolingStatus //EN Ultimate schooling results for children
{
	school_status +
		*
	{
		undergone_transition(alive, TRUE, FALSE) //EN Number of Children
	}
};

table Child ChildSchoolingUndergone //EN Number of child years spent in school
{
	integer_age +
		*
	{
		duration(school_status, IN_SCHOOL), //EN Number of school years
			duration() //EN Number of child years
	}
};

table Child ChildAbuseOutcomes //EN Abuse statistics
	[integer_age >= 12 && integer_age <= 18]
{
//	integer_age +
	//	*
	{
		unit, //EN Susceptible Children
		undergone_transition(abused, FALSE, TRUE), //EN Abused Children
		duration(), //EN Susceptible Child Years
		duration(abused, TRUE) //EN Abused Child Years
	}
};

table Child ChildDepressionOutcomes //EN Depression statistics
	[integer_age >= 12 && integer_age <= 18]
{
//	integer_age +
	//	*
	{
		unit, //EN Susceptible Children
		undergone_transition(depressed, FALSE, TRUE), //EN Depressed Children
		duration(), //EN Susceptible Child Years
		duration(depressed, TRUE) //EN Depressed Child Years
	}
};

table Child ChildCondomUsageOutcomes //EN Condom Usage statistics
	[integer_age >= 13 && integer_age <= 18]
{
//	integer_age +
	//	*
	{
		duration(consistent_condom_user, FALSE), //EN Inconsistent Condom Usage Child Years
		duration(consistent_condom_user, TRUE) //EN Consistent Condom Usage Child Years
	}
};

table_group Child_General
{
	ChildSurvival,
	ChildMotherStateAtDropout,
	ChildFinalSchoolingStatus,
	ChildSchoolingUndergone,
	ChildAbuseOutcomes,
	ChildDepressionOutcomes,
	ChildCondomUsageOutcomes
};

/* ################################################################################
The Child Rates tables
################################################################################ */

table Child ChildDropoutRates //EN Dropout Rates of Children
{
	gender +
		*
	ses +
	    *
	child_sched_status +
		*
	integer_age +
	*
	{
		unit, //EN Number of Children who entered the state
		duration(), //EN Total Number of Child years
		duration(school_status, IN_SCHOOL), //EN Total number of Child years spent IN_SCHOOL
		undergone_transition(school_status, IN_SCHOOL, DROPOUT), //EN Dropouts from state
		undergone_transition(school_status, IN_SCHOOL, DROPOUT) / duration(school_status, IN_SCHOOL) //EN Dropout Rate decimals=4
	}
};

table Child ChildAbuseRates //EN Abuse Rates of Children
{
	gender +
		*
	ses +
	    *
	child_sched_status +
		*
	integer_age +
	*
	{
		unit, //EN Number of Children who entered the state
		duration(), //EN Total number of Child years spent in state
		duration(abused, FALSE), //EN Number of unabused Child years spent in state
		duration(abused, TRUE), //EN Number of abused Child years spent in state
		undergone_transition(abused, FALSE, TRUE), //EN Number of children who entered abused state
		undergone_transition(abused, FALSE, TRUE) / duration(abused, FALSE) //EN Abuse state entrace rate decimals=4
	}
};

table Child ChildDepressionRates //EN Depression Rates of Children
{
	abused +
		*
	gender +
		*
	ses +
	    *
	child_sched_status +
		*
	integer_age +
		*
	{
		unit, //EN Number of Children who entered the state
		duration(), //EN Total number of Child years spent in state
		duration(depressed, FALSE), //EN Number of undepressed Child years spent in state
		duration(depressed, TRUE), //EN Number of depressed Child years spent in state
		undergone_transition(depressed, FALSE, TRUE), //EN Number of children who entered depressed state
		undergone_transition(depressed, FALSE, TRUE) / duration(depressed, FALSE) //EN Depressed state entrace rate decimals=4
	}
};

/* NOTE (ChildCondomUsageRates, EN)
This table is really hard to interpret. The values in the Number of Children column for age 0 constains double counted children. 
subsequent ages are more accurate. The Number of Child Years Column is also more accurate
*/

table Child ChildCondomUsageRates //EN Condom Usage Rates of Children
{
	integer_age +
	*
	gender +
	*
	consistent_condom_user +
	*
	{
		unit, //EN Number of Children
		duration() //EN Number of Child years
	}
};


table_group Child_Rates
{
	ChildDropoutRates,
	ChildAbuseRates,
	ChildDepressionRates,
	ChildCondomUsageRates
};

/* ################################################################################
The State Membership tables
################################################################################ */

table Child ChildMotherMDSMembershipxAge //EN Membership of children's Mothers in the MDEATH_SCHED_STATE
{
	integer_age+ //EN Age
		*
	{
		duration(mdeath_sched_status, mdsHEALTHY), //EN Mother years in HEALTHY state decimals=2
		duration(mdeath_sched_status, mdsINFECTED), //EN Mother years in INFECTED state decimals=2
		duration(mdeath_sched_status, mdsTREATED), //EN Mother years in TREATED state decimals=2
		duration(mdeath_sched_status, mdsSTOPPED), //EN Mother years in STOPPED state decimals=2
		duration(mdeath_sched_status, mdsDEAD) //EN Mother years in DEAD state decimals=2
	} //EN Membership counts
};

table Child ChildMotherMDMembershipxAge //EN Membership of children's Mothers in the MDETAILED_STATE
{
	integer_age+ //EN Age
		*
	{
		duration(mdetailed_status, mdHEALTHY), //EN Mother years in HEALTHY state decimals=2
		duration(mdetailed_status, mdINFECTED), //EN Mother years in INFECTED state decimals=2
		duration(mdetailed_status, mdSYMPTOMS), //EN Mother years in SYMPTOMS state decimals=2
		duration(mdetailed_status, mdDIAGNOSED), //EN Mother years in DIAGNOSED state decimals=2
		duration(mdetailed_status, mdDIAGSYMP), //EN Mother years in DIAGSYMP state decimals=2
		duration(mdetailed_status, mdTREATED), //EN Mother years in TREATED state decimals=2
		duration(mdetailed_status, mdSTOPPED), //EN Mother years in STOPPED state decimals=2
		duration(mdetailed_status, mdDEAD) //EN Mother years in DEAD state decimals=2
	} //EN Membership counts
};

table Child ChildMotherCSMembershipxAge //EN Membership of children's Mothers in the CHILD_SCHED_STATE
{
	integer_age+ //EN Age
		*
	{
		duration(child_sched_status, cHEALTHY), //EN Mother years in HEALTHY state decimals=2
		duration(child_sched_status, cINFECTED), //EN Mother years in INFECTED state decimals=2
		duration(child_sched_status, cSYMPTOMS), //EN Mother years in SYMPTOMS state decimals=2
		duration(child_sched_status, cTREATED), //EN Mother years in TREATED state decimals=2
		duration(child_sched_status, cSTOPPED), //EN Mother years in STOPPED state decimals=2
		duration(child_sched_status, cDEAD) //EN Mother years in DEAD state decimals=2
	} //EN Membership counts
};

table Child SchoolChildMotherMDMembershipxAge //EN Membership of school children's Mothers in the MDETAILED_STATE
	[school_status == IN_SCHOOL]
{
	integer_age+ //EN Age
		*
	{
		duration(mdetailed_status, mdHEALTHY), //EN Mother years in HEALTHY state decimals=2
		duration(mdetailed_status, mdINFECTED), //EN Mother years in INFECTED state decimals=2
		duration(mdetailed_status, mdSYMPTOMS), //EN Mother years in SYMPTOMS state decimals=2
		duration(mdetailed_status, mdDIAGNOSED), //EN Mother years in DIAGNOSED state decimals=2
		duration(mdetailed_status, mdDIAGSYMP), //EN Mother years in DIAGSYMP state decimals=2
		duration(mdetailed_status, mdTREATED), //EN Mother years in TREATED state decimals=2
		duration(mdetailed_status, mdSTOPPED), //EN Mother years in STOPPED state decimals=2
		duration(mdetailed_status, mdDEAD) //EN Mother years in DEAD state decimals=2
	} //EN Membership counts
};

table_group Child_State_Membership
{
	ChildMotherMDSMembershipxAge,
	ChildMotherMDMembershipxAge,
	SchoolChildMotherMDMembershipxAge,
	ChildMotherCSMembershipxAge
};
